#!/usr/bin/env groovy
import groovy.transform.Field

/**
 * Velocity Banking - Multi-Architecture GitOps Pipeline
 */

@Field def config = [
    registry: '192.168.68.124:30501',
    appName: 'velocity-banking',
    manifestRepoUrl: 'https://github.com/naren-m/homelab.git',
    gitCredentialsId: 'github-credentials',
    manifestRelativePath: 'base/applications/velocity-banking',
    namespace: 'velocity-banking',
    kubeconfigId: 'homelab-kubeconfig',
    platforms: 'linux/amd64',
    builderName: 'homelab-builder'
]

@Field def imageTag = ''

pipeline {
    agent { label 'docker-buildx' }

    options {
        buildDiscarder(logRotator(numToKeepStr: '10'))
        disableConcurrentBuilds()
        timestamps()
    }

    environment {
        BACKEND_IMAGE = "${config.registry}/${config.appName}-backend"
        FRONTEND_IMAGE = "${config.registry}/${config.appName}-frontend"
        DOCKER_BUILDKIT = '1'
    }

    stages {
        stage('Preparation') {
            steps {
                script {
                    def ts = new Date().format("HHmmss-ddMMMyy")
                    def gitHash = sh(script: "git rev-parse --short HEAD", returnStdout: true).trim()
                    imageTag = "${ts}-${gitHash}"
                    echo "ðŸš€ Starting build for ${config.appName} with tag: ${imageTag}"
                }
            }
        }

        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Setup Buildx') {
            steps {
                script {
                    sh """
                        mkdir -p /tmp/buildx-config
                        cat > /tmp/buildx-config/buildkitd.toml << 'TOML'
[registry."192.168.68.124:30501"]
  http = true
  insecure = true
TOML
                        if docker buildx inspect ${config.builderName} >/dev/null 2>&1; then 
                            echo "Builder exists"
                        else
                            docker buildx create --name ${config.builderName} --driver docker-container --config /tmp/buildx-config/buildkitd.toml --use --bootstrap
                        fi
                    """
                }
            }
        }

        stage('Build & Push Images') {
            parallel {
                stage('Backend') {
                    steps {
                        dir('backend_python') {
                            sh "docker buildx build --platform ${config.platforms} --tag ${BACKEND_IMAGE}:${imageTag} --tag ${BACKEND_IMAGE}:latest --builder ${config.builderName} --push ."
                        }
                    }
                }
                stage('Frontend') {
                    steps {
                        dir('frontend') {
                            sh "docker buildx build --platform ${config.platforms} --tag ${FRONTEND_IMAGE}:${imageTag} --tag ${FRONTEND_IMAGE}:latest --builder ${config.builderName} --push ."
                        }
                    }
                }
            }
        }

        stage('Update GitOps') {
            steps {
                script {
                    dir('homelab-gitops') {
                        checkout([
                            $class: 'GitSCM', 
                            branches: [[name: 'main']], 
                            userRemoteConfigs: [[url: config.manifestRepoUrl, credentialsId: config.gitCredentialsId]]
                        ])
                        
                        sh """
                            sed -i "s|image: .*${config.appName}-backend:.*|image: ${BACKEND_IMAGE}:${imageTag}|g" ${config.manifestRelativePath}/backend-deployment.yaml
                            sed -i "s|image: .*${config.appName}-frontend:.*|image: ${FRONTEND_IMAGE}:${imageTag}|g" ${config.manifestRelativePath}/frontend-deployment.yaml
                            git config user.email "jenkins@hanuma.com"
                            git config user.name "Jenkins CI"
                            git add .
                            if git commit -m "chore(${config.appName}): update images to ${imageTag} [skip ci]"; then
                                git push origin main
                            fi
                        """
                    }
                }
            }
        }

        stage('Sync & Verify') {
            steps {
                script {
                    def kubectlPath = "${WORKSPACE}/bin/kubectl"
                    sh "mkdir -p ${WORKSPACE}/bin && [ ! -x ${kubectlPath} ] && curl -sLO https://dl.k8s.io/release/v1.32.0/bin/linux/amd64/kubectl && chmod +x kubectl && mv kubectl ${kubectlPath} || true"
                    
                    withKubeConfig([credentialsId: config.kubeconfigId]) {
                        sh "${kubectlPath} patch app homelab-applications -n argocd --type merge -p '{\"operation\":{\"initiatedBy\":{\"username\":\"jenkins\"},\"sync\":{\"revision\":\"HEAD\"}}}'"
                        sh "${kubectlPath} rollout status deployment/${config.appName}-backend -n ${config.namespace} --timeout=300s"
                        sh "${kubectlPath} rollout status deployment/${config.appName}-frontend -n ${config.namespace} --timeout=300s"
                    }
                }
            }
        }
    }

    post {
        always {
            cleanWs()
        }
    }
}
